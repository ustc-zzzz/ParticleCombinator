buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'minecrell'
            url = 'http://repo.minecrell.net/releases'
        }
        dependencies {
            classpath 'net.minecrell:VanillaGradle:2.0.3_1'
            classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        }
    }
}

apply plugin: 'net.minecrell.vanilla.server.library'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def getGitHash = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getReleaseDate = {
    def format = new java.text.SimpleDateFormat('yyyy-MM-dd') // RFC 3339
    return format.format(new Date())
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
//  maven {
//      name = 'jitpack'
//      url = 'https://jitpack.io'
//  }
}

version = '0.2.1'
archivesBaseName = 'ParticleEffectCombinator'

minecraft {
    version = '1.10.2'
    mappings = 'snapshot_20161111'

    replace '@version@', project.version
    replace '@git_hash@', getGitHash()
    replace '@release_date@', getReleaseDate()
}

task apiJar(type: Jar, dependsOn: sourceJar) {
    from 'build/sources/api/java/'
    baseName = 'ParticleEffectCombinatorAPI'
    
    from 'api/LICENSE'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from 'build/docs/javadoc/'
    classifier = 'javadoc'
}

artifacts {
    archives apiJar
    archives sourceJar
    archives javadocJar
}

dependencies {
    compile 'org.spongepowered:spongeapi:5.2.0-SNAPSHOT'
    compile 'org.spongepowered:spongecommon:5.2.0-SNAPSHOT:dev'
//  compile 'com.github.rojo8399:PlaceholderAPI:master-SNAPSHOT'
}

sourceSets {
    main {
        java.srcDir 'src/'
        java.srcDir 'api/'
        resources.srcDir 'resources/'
    }
    api {
        java.srcDir 'api/'
    }
}

reobf {
    jar {
        mappingType = 'SEARGE'
    }
}

